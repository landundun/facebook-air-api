<?xml version='1.0' encoding='utf-8'?>

<!--
Copyright (c) 2010, Adobe Systems Incorporated
All rights reserved.

Redistribution and use in source and binary forms, with or without 
modification, are permitted provided that the following conditions are
met:

* Redistributions of source code must retain the above copyright notice, 
this list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright
notice, this list of conditions and the following disclaimer in the 
documentation and/or other materials provided with the distribution.

* Neither the name of Adobe Systems Incorporated nor the names of its 
contributors may be used to endorse or promote products derived from 
this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
-->


<s:WindowedApplication xmlns:fx='http://ns.adobe.com/mxml/2009' 
					   xmlns:s='library://ns.adobe.com/flex/spark'
					   xmlns:mx='library://ns.adobe.com/flex/mx'
					   width="640" height="480"
					   creationComplete='init()'
					   >
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:layout>
		<s:VerticalLayout />
	</s:layout>
	
	<fx:Script>
		<![CDATA[
			import com.facebook.graph.FacebookDesktop;
			import com.facebook.graph.controls.Distractor;
			import com.facebook.graph.net.FacebookRequest;
			
			import spark.layouts.VerticalLayout;
			
			protected var fileRef:File;
			protected var imgFilter:FileFilter;
			protected var distractor:Distractor;
			protected var bmd:BitmapData;
			protected var isSnapshot:Boolean = false;
			
			protected function init():void {
				
				if (Camera.isSupported) {
					vidDisplay.visible = vidDisplay.includeInLayout = true;
					snapshotBtn.visible = snapshotBtn.includeInLayout = true;
					vidDisplay.attachCamera(Camera.getCamera());
					bmd = new BitmapData(vidDisplay.width, vidDisplay.height);
				} else {
					vidDisplay.visible = vidDisplay.includeInLayout = false;
					snapshotBtn.visible = snapshotBtn.includeInLayout = false;
				}
				
				fileRef = new File();
				fileRef.addEventListener(Event.SELECT, handleFileSelect, false, 0, true);
				
				imgFilter = new FileFilter('Images', '*.jpg; *.gif; *.png');
				
				distractor = new Distractor();
				distractorWrapper.addChild(distractor);
				distractor.text = 'Uploading';
				distractor.x = width - distractor.width >> 1;
				distractor.visible = false;
				
				browseBtn.enabled = false;
				snapshotBtn.enabled = false;
				uploadBtn.enabled = false;
				logoutBtn.enabled = false;
				
				status = 'Click "login" to start';
				
				FacebookDesktop.init('YOUR_APPLICATION_ID', handleLogin);
			}
			
			protected function login():void {
				FacebookDesktop.login(handleLogin, []);
			}
			
			protected function logout():void {
				FacebookDesktop.logout();
			}
			
			protected function browseForUpload():void {
				fileRef.browse([imgFilter]);
			}
			
			protected function getSnapshot():void {
				bmd.draw(vidDisplay);
				previewImage.source = new Bitmap(bmd);
				uploadBtn.enabled = true;
				isSnapshot = true;
			}
			
			protected function upload():void {
				status = '';
				var img:* = (isSnapshot) ? bmd : fileRef;
				var values:Object = {message:'My photo caption', fileName:'FILE_NAME',image:img};
				
				FacebookDesktop.api('/me/photos', handleUploadComplete, values,'POST');
				distractor.visible = true;
			}
			
			
			protected function handleLogin(response:Object, fail:Object):void {
				if (response) {
					browseBtn.enabled = true;
					snapshotBtn.enabled = true;
					loginBtn.enabled  = false;
					logoutBtn.enabled = true;
					
					status = 'Click "browse" to select photo or "snapshot" to capture webcam image';
				}
			}
			
			protected function handleLogout(response:Object, fail:Object):void {
				if (response) {
					mediaName.text =  '';
					distractor.visible = false;
					previewImage.source = null;
					
					browseBtn.enabled = false;
					snapshotBtn.enabled = false;
					uploadBtn.enabled = false;
					loginBtn.enabled  = true;
					logoutBtn.enabled = false;
					
					status = 'Click "login" to start';
				}
			}
			
			protected function handleFileSelect(event:Event):void {
				mediaName.text =  fileRef.nativePath;
				previewImage.source = 'file:///' + fileRef.nativePath;
				uploadBtn.enabled = true; 
				isSnapshot = false;
			}
			
			protected function handleUploadComplete(response:Object, fail:Object):void {
				mediaName.text =  '';
				distractor.visible = false;
				previewImage.source = null;	
				status = (response) ? 'Successfully uploaded' : 'Error uploading';	
			}
			
		]]>
	</fx:Script>
	
	<s:HGroup width="100%">
		<s:TextInput id="mediaName" width="250" />	
		<s:Button id="browseBtn" label="Browse" click="browseForUpload()" />
		<s:Button id="snapshotBtn" label="Snapshot" click="getSnapshot()" />
		<s:Button id="uploadBtn" label="Upload" click="upload()" />
		<s:Button id="loginBtn" label="Login" click="login()" />
		<s:Button id="logoutBtn" label="Logout" click="logout()" />
	</s:HGroup>
	
	<s:HGroup width="100%" verticalAlign="top">
		<mx:VideoDisplay id="vidDisplay" width="160" height="120" />
		<mx:Image id="previewImage" width="400" height="300" maintainAspectRatio="true"/>
	</s:HGroup>
	
	<mx:UIComponent id="distractorWrapper" width="100%"/>
	
</s:WindowedApplication>